label ch3_main:
    scene bg club_day
    with dissolve_scene_half
    play music t3
    show monika 1g at l31
    m "Ah, dostum..."
    m "Yine en son ben geldim!"
    mc "Merak etme, ben de yeni geldim."
    show yuri 1f at f32 zorder 3
    y "Yine piyano mu çalışıyordun?"
    show yuri at t32 zorder 2
    show monika at f31 zorder 3
    m 1l "Evet..."
    m "Ahaha..."
    show monika at t31 zorder 2
    show yuri at f32 zorder 3
    y 1m "Çok kararlı olmalısınız."
    y "Bu kulübe başlamak ve şimdi de piyano çalmak..."
    show yuri 1a at t32 zorder 2
    show monika at f31 zorder 3
    m 1a "Belki kararlılık değil..."
    m "Ama sanırım tutku."
    m "Unutmayın ki sizler olmasaydınız bu kulüp burada olmazdı."
    m 1b "Ve hepinizin festival için yardım etmeye istekli olmanızdan da çok mutluyum!"
    show natsuki 1z at f33 zorder 3
    show monika at t31 zorder 2
    n "Aaah, festival için sabırsızlanıyorum!"
    n "Harika olacak!"
    show natsuki at t33 zorder 2
    show monika at f31 zorder 3
    m 1d "Eh?"
    m "Daha dün bundan şikayetçi değil miydin, Natsuki?"
    show monika at t31 zorder 2
    show natsuki 2a at f33 zorder 3
    n "Şey, evet."
    n "Festivalin {i}bizim{/i} bölümünden bahsetmiyorum."
    n 4l "Ama okulda oyun oynadığımız ve her türlü lezzetli yemeği yediğimiz koca bir gün!"
    show natsuki at t33 zorder 2
    mc "Birdenbire Sayori gibi konuşmaya başladın..."
    show natsuki at f33 zorder 3
    n 4a "Monika! Genelde kızarmış kalamar var mı?"
    show natsuki at t33 zorder 2
    show monika 2a at f31 zorder 3
    m "Kalamar...?"
    m "Bu dört gözle beklenecek oldukça özel bir şey..."
    show monika at t31 zorder 2
    show natsuki 2k at f33 zorder 3
    n "Oh, hadi ama."
    n "Kalamar sevmediğini mi söylüyorsun?"
    n "Tüm insanlar arasında, sen?"
    show natsuki at t33 zorder 2
    show monika 1d at f31 zorder 3
    m "Ne? Sevmediğimi söylemedim."
    m "Ayrıca, 'tüm insanlar arasında sen' derken neyi kastediyorsun?"
    show monika at t31 zorder 2
    show natsuki 1d at f33 zorder 3
    n "Çünkü!"
    n "Bu senin adında var!"
    n 4z "Mon-ika!"
    show natsuki at t33 zorder 2
    show monika 5b at f31 zorder 3
    m "Eh?!"
    m "İsmimi hiç de öyle söylemiyorsun!"
    m "Ayrıca, bu şaka çeviride hiçbir anlam ifade etmiyor!"
    show monika at t31 zorder 2
    show natsuki at f33 zorder 3
    n 4m "...?"
    show natsuki at t33 zorder 2
    show monika 4l at f31 zorder 3
    m "Ah... boş ver!"
    m "Şimdilik sadece kendi etkinliğimize odaklanalım, tamam mı?"
    show monika at t31 zorder 2
    show natsuki 2a at f33 zorder 3
    n "Ehehe."
    n "Tamam, tamam."
    n "Senin tepkilerin Yuri'ninki ya da Sayori'ninki kadar eğlenceli değil zaten."
    show natsuki at t33 zorder 2
    show yuri 2h at f32 zorder 3
    y "Affedersin..."
    show yuri at t32 zorder 2
    mc "Sayori nerede bu arada...?"
    mc "Oh, işte buradasın."
    hide monika
    hide yuri
    hide natsuki
    with wipeleft
    "Sayori odanın köşesindeki masada oturmuş, hiçbir şeye bakmıyor."
    "Ona doğru yürüdüm."
    mc "Hey, Sayori." #ah benim üzümlü kekim...
    show sayori 1k at t11 zorder 2
    "Elimi yüzünün önünde salladım."
    s 1n "Eh--?"
    mc "Yine dalgınsın."
    s "A-Ah..."
    s 4l "Ehehe, üzgünüm..."
    s "Bana aldırma."
    s 4y "Gidip diğerleriyle konuşabilirsin."
    mc "Huh..."
    mc "Her şey yolunda mı?"
    s 1h "Tabii ki!"
    s "Neden olmasın ki?"
    mc "Sadece biraz garip hissediyorum..."
    mc "Varsayımlarda bulunduğum için özür dilerim."
    s "Tanrım, benim için çok fazla endişeleniyorsun."
    s 4r "Ben iyiyim, gördün mü?" #ben kötü bir insanım.
    show sayori at h11
    "Sayori bana kocaman bir gülümseme gösteriyor."
    s 1a "Herkesle eğlenirken dikkatini dağıtmama izin verme."
    mc "Peki...tamam."
    mc "Sen öyle diyorsan."
    show sayori at thide zorder 1
    hide sayori
    "Diğerlerine dönmeden önce endişeyle Sayori'ye bakıyorum."
    "Ama sohbet çoktan dağılmış, herkes olağan faaliyetlerine geri dönmüştü."
    "Belki de Monika'ya son zamanlarda Sayori ile ilgili bir şey fark edip etmediğini sormalıyım..."
    "Festivale hazırlandıklarına göre, birlikte çok zaman geçiriyor olmalılar."
    "Masasında bazı kağıtları karıştıran Monika'ya çekingen bir şekilde yaklaştım."
    show monika 2b at t11 zorder 2
    m "[player]! N'aber?"
    mc "Hey, bu biraz garip gelebilir ama..."
    mc "Son zamanlarda Sayori ile ilgili bir şey fark ettin mi?"
    m 1d "Onunla ilgili bir şey...?"
    m "Nasıl yani?"
    mc "Belki biraz fazla yorumluyorum ama bugün biraz keyifsiz görünüyor..."
    m "Oh? Öyle mi düşünüyorsun?"
    m "Onunla ilgili bir şey fark ettiğimi söyleyemem..."
    "Monika odanın öbür ucunda, masasında lastik silgiyi bir aşağı bir yukarı sürükleyen Sayori'ye bakıyor."
    m "Belki de aklında bir şeyler vardır..."
    m 2a "Ama bunu sana soranın ben olmamama şaşırdım, [player]."
    m "Onu benden çok daha iyi tanıdığın kesin."
    mc "Evet, ama o hiç böyle değildi..."
    mc "Onu rahatsız eden şeyler hakkında benimle hep konuşurdu."
    mc "Ama bu sefer, ona sorduğumda, gerçekten umursamazdı."
    mc "...Üzgünüm, bunun senin sorunun olmadığını biliyorum!"
    mc "Sadece bir şey bilip bilmediğini sormak istemiştim, o yüzden şimdi bırakıyorum..."
    m 1g "Hayır, hayır..."
    m "Bu benim için de önemli."
    m 1e "Yani, ben de onun arkadaşıyım..."
    m "Ayrıca kulüp üyelerimin iyiliğini de önemsiyorum, anlıyor musun?"
    m 1i "Belki onunla kendim konuşmayı denerim..."
    mc "Eh? Bundan emin misin...?"
    mc "Yalnız kalmak istiyor gibi görünüyordu..."
    m "Emin misin?"
    m "Belki de bu konuyu ilgili kişiye açmakta zorlanıyordur..."
    mc "İlgili kişi...?"
    mc "Bununla ne demek istiyorsun?"
    m 2e "Diyorum ki belki de aklında olan şey sensindir, [player]."
    mc "Ben...?"
    mc "Bu sonuca nasıl vardın?"
    m 1j "Şey..."
    m "Muhtemelen çok fazla şey söylememeliyim ama..."
    m 1a "Sayori her şeyden çok senin hakkında konuşuyor, biliyor musun?"
    mc "Eh...?"
    m "Sen kulübe katıldığından beri çok daha mutlu."
    m "Sanki içinde fazladan bir ışık yanıyor."
    mc "Ne?"
    mc "Olamaz..."
    mc "Sayori...her zaman böyledir."
    mc "O her zaman güneş ışığıyla doluydu."
    mc "Şimdi de her zaman olduğundan farklı değil."
    m 5 "Ehehe."
    m "Çok komiksin, [player]."
    m "Belki de onu her zaman çok neşeli gördüğünü düşündün mü..."
    m "...çünkü senin yanındayken öyle olduğu için mi?"
    mc "..."
    m 1n "Ah... Çok fazla şey söyledim."
    m "Özür dilerim... Ben ne bilirim ki?"
    m 1a "Hemen sonuca varmak istememiştim, bu yüzden söylediklerimi unutmalısın."
    m "Onunla konuşmaya çalışacağım, o yüzden şimdilik bunu düşünmemeye çalış."
    mc "Ah..."
    mc "Pekala..."
    "Monika anlamlı bir şekilde gülümser."
    "Bunu unutmamı söylediğini biliyorum..."
    "Ama onun sözlerini aklımdan çıkaramayacağımı şimdiden biliyorum."
    show monika at thide zorder 1
    hide monika
    "Monika masasından kalkıyor ve odanın diğer ucuna, Sayori'nin oturduğu yere doğru yürüyor."
    "Sayori'nin yanına diz çöküp onunla nazikçe konuşmasını izliyorum."
    "Ama sesini o kadar alçak tutuyor ki buradan onu duyamıyorum."
    "İçimi çekiyorum ve oturuyorum."
    "Sayori'nin bana onun için endişelenmememi ve diğer herkesle eğlenmemi söylediğini biliyorum..."
    "Ama o böyle davranırken bunu yapmak imkansız."
    "Onu tam olarak ne kadar önemsiyorum ki, bunun beni bu kadar yormasına izin veriyorum?"
    "Şimdi sıra dışı davranan benmişim gibi geliyor..."
    "Ama Monika'yı beklemekten başka yapabileceğim bir şey yok."

    # Natsuki or Yuri will come up to you depending on whose appeal is higher
    # If tied, the winner of chapter 2 comes up to you
    # If neither won chapter 2, the winner of chapter 1 comes up to you
    # If neither have any appeal, then neither come up to you
    if n_appeal == 0 and y_appeal == 0:
        jump ch3_start_none
    elif n_appeal > 1:
        jump ch3_start_natsuki
    elif y_appeal > 1:
        jump ch3_start_yuri
    elif poemwinner[1] == "natsuki":
        jump ch3_start_natsuki
    elif poemwinner[1] == "yuri":
        jump ch3_start_yuri
    elif poemwinner[0] == "natsuki":
        jump ch3_start_natsuki
    elif poemwinner[0] == "yuri":
        jump ch3_start_yuri
    else:
        jump ch3_start_none

label ch3_start_natsuki:
    play music t6 fadeout 1
    show natsuki 3c at t11 zorder 2
    n "Hey, sen."
    mc "Eh?"
    "Kafamı kaldırdığımda Natsuki'nin yanımda olduğunu gördüm."
    n "Orada öylece oturup boş boş bakmaya devam mı edeceksin?"
    n "Çok fazla zaman yok, o yüzden..."
    mc "Ah, özür dilerim."
    mc "Seni endişelendirmek istememiştim."
    n 1q "Endişelendiğimden değil!"
    n "Ben sadece..."
    "Natsuki yan tarafına bakar."
    "Elinde bir manga cildi tutuyor."
    mc "Doğru..."
    mc "Bir dakikalığına bir şey çıktı, ama şimdi başlayabiliriz."
    mc "Seni daha fazla bekletmeyeceğim."
    n 5n "Tanrım..."
    n "Şimdi kendimi bir pislik gibi hissettiriyorsun."
    n "Eğer seni rahatsız eden bir şey varsa, o zaman bana seni yalnız bırakmamı söyleyebilirsin, ben de bırakırım."
    n 5u "Yani..."
    n "Bunun hakkında konuşmak istemediğini varsayarsak..."
    "Son kısmı neredeyse mırıldanıyor."
    mc "Hayır... Muhtemelen olduğundan daha büyük bir mesele gibi gösteriyorum."
    mc "Sadece Sayori'yi düşünüyordum, hepsi bu."
    n 1p "S-Sayori...?"
    "Onu düşünüyorsun...?"
    mc "Evet, bugün oldukça üzgün görünüyor."
    mc "Ama bunu bana itiraf etmek istemedi."
    mc "Bu yüzden ona bir şey mi oldu diye düşünmeden edemiyorum."
    n 1q "Oh..."
    "Natsuki nefes verir."
    n 4w "Şey, her şeyden önce..."
    n "İfadelerin üzerinde gerçekten çalışmalısın!"
    n 4c "Ama her neyse..."
    n "Sen onun en iyi arkadaşısın, değil mi?"
    mc "Evet, sanırım öyle..."
    n "Evet."
    n 3k "O halde, bence ona biraz daha güvenmelisin."
    n "Eğer sana ihtiyacı olsaydı, gideceği ilk kişi sen olurdun, değil mi?"
    mc "Şey... Sanırım bu doğru."
    n 3c "Yani, bazı insanların öyle günleri olur."
    n "Her zaman bundan kaçınamazsın."
    n "Eğer bir şey varsa, muhtemelen onun hakkında endişelenmenizi istemiyordur çünkü önemli değildir."
    mc "Evet, bana da öyle demişti..."
    mc "Belki de onun isteklerine karşı gelmek benim için doğru değildir."
    n 3a "Kesinlikle."
    n "Eğer onun için endişelenmene ihtiyacı varsa, o zaman bu çok daha açık olacaktır."
    mc "Evet..."
    mc "Başından beri bu şekilde düşünmeliydim."
    show natsuki 3q
    "Natsuki elinde tuttuğu kitapla oynuyor."
    n 1q "O..."
    n "O senin için gerçekten çok şey ifade ediyor, değil mi...?"
    mc "Ah--"
    mc "Yanlış bir fikre kapılmayın...!"
    mc "Biz sadece uzun zamandır arkadaşız."
    mc "Arkadaşların için endişelenmen normal."
    mc "Yani, sen de benim için endişeleniyordun, yani--"
    n 4w "Değildim!!"
    n "Tanrım... eğer iyiysen, o zaman acele edelim ve başlayalım!"
    mc "Evet, evet..."

    # If we haven't seen her exclusive yet, play it now
    if not n_exclusivewatched and poemwinner[2] == "natsuki":
        call natsuki_exclusive_2_ch3
    else:
        jump ch3_start_none
    return

label ch3_start_yuri:
    "Neden izleniyormuşum gibi hissediyorum...?"
    "Odanın etrafına bakıyorum."
    show yuri 2t at t11 zorder 2
    "Birden Yuri'nin kitabının üzerinden bana baktığını fark ettim."
    show yuri 4b
    "Ama yüzünde telaşlı bir ifadeyle aynı hızla gözlerini kaçırıyor."
    "Bu şekilde bir yere varamayacağının farkındayım."
    "Yuri'nin kimseye yaklaştığını ya da kendi başına bir konuşma başlattığını hiç görmedim."
    "Bu yüzden ona kendim yaklaşmaktan başka çarem yok."
    "Şimdiye kadar bunu yapmak benim için biraz daha kolay oldu."
    "Kendi masamdan kalkıp onunkinin yanına oturuyorum."
    play music t6 fadeout 1
    y 2v "..."
    y "Ben... sizi rahatsız etmek istemedim..."
    mc "Sakin ol, bir şey yapmadın bile."
    y "Ama..."
    y "Düşüncelerinle yalnız kalmak istediğini söyleyebilirim..."
    mc "Düşüncelerimle yalnız...?"
    mc "Böyle düşündüğümü nasıl anladın?"
    y 1t "Şey..."
    y "Bu benim çok yaptığım bir şey..."
    y "Yani duruşunuzdan ve yüz ifadenizden bunu anlamak benim için zor olmadı."
    y 3o "Baktığımdan falan değil...!"
    y "Öyle ürkütücü bir şey yapmadım...!"
    mc "Her halükarda, sanırım haklıydın."
    mc "Sizi endişelendirdiysem özür dilerim."
    y 1s "Özür dileme..."
    y "Dertleriniz yalnızca bu dertleri paylaşmaya gönüllü olanları ilgilendirir."
    y "Elbette, en büyük rahatlığı kendi içlerine kapanmakta bulanlar da var..."
    y "Ama aklınızdan geçenleri paylaşmayı tercih ederseniz, dinlemekten memnuniyet duyarım."
    mc "Ah, gerçekten o kadar da büyük bir mesele değil..."
    mc "Sadece Sayori hakkında biraz huzursuz hissediyordum."
    y 2t "Sayori...?"
    mc "Evet... bugün biraz keyifsiz görünüyor, ama bunu ona sorduğumda bana itiraf etmek istemedi."
    mc "Bu yüzden ona bir şey mi oldu diye düşünmeden edemiyorum."
    y 3u "Oh?"
    y "Bu oldukça romantik..."
    mc "Eh...?"
    y 4c "Özür dilerim!"
    y "Aptalca bir şey söylemek istememiştim...!"
    mc "Öyle değil, sadece yanlış anlamanı istemedim."
    mc "Sayori ve ben sadece uzun zamandır arkadaşız, hepsi bu."
    y 2l "Ah... Anlıyorum..."
    y 2f "O zaman belki de duyguları konusunda size karşı umursamaz davranması alışılmadık bir durumdur..."
    mc "Ya da belki de ben biraz fazla yorumluyorum..."
    y 1u "[player]..."
    y "Dünya anlamlarla doludur, çoğu zaman gözün göremeyeceği derinliklerde gizlidir."
    y 1s "Ve ne kadar iyi tanıyor olursanız olun, her insanın ardında anlatılmamış pek çok gizem vardır."
    mc "Ah..."
    mc "Yani her şeye rağmen bunun arkasında bir şeyler olabileceğini mi düşünüyorsunuz?"
    y 1l "Mm..."
    y "Sayori'nin çok karmaşık bir insan olduğunu düşünüyorum."
    y 1h "Dışarıdaki tavırları kafasının içinde olup bitenlerle her zaman uyuşmayabilir..."
    y "Ve her zaman ne istediğini bilmiyor olabilir."
    y "Bugünkü garip davranışlarını ben de fark ettim..."
    y "Ve ayrıca onun için biraz endişe duyuyorum."
    y 1f "Ama sizin durumunuzda, düşüncelerinizi tamamen işgal ediyor gibi görünüyordu, değil mi?"
    mc "Şey..."
    mc "Sanırım durum buydu."
    y 3u "Sayori..."
    y "O senin için gerçekten çok şey ifade ediyor, değil mi?"
    mc "Ah--Ben...Sanırım..."
    mc "Ama bu şekilde ifade etmene gerek yok!"
    mc "Biz sadece iyi arkadaşız, hepsi bu..."
    y 2t "..."
    "Yuri birden gözlerimin içine baktı."
    "Yüz ifadesi nazik ve meraklı, sanki bir şey arıyormuş gibi."
    "Utandım, bakışlarımı kaçırdım."
    y 2m "Bazen..."
    y "Bir insanın gizemleri kendisine bile anlatılamaz."
    y 2a "Ve sen, dürüst ve şefkatli biri olarak..."
    y "İçinde olduğunu bilmediğin duyguları ortaya çıkarabilirsin."
    y 4b "Bu..."
    y "Bence..."
    y "O çok şanslı bir insan olurdu..."
    y "Onun hakkında böyle hissettiğin için..."
    mc "Yuri..."
    mc "Bana çok fazla kredi veriyorsun."
    mc "Ben oldukça basit bir adamım."
    mc "Bu yüzden kendi duygularımı anlamakta oldukça iyi olduğumu düşünüyorum."
    mc "Senin kadar sofistike değilim."
    y 2t "A-Ah..."
    y "Bu... bir iltifat değil, değil mi?"
    mc "Neyse o."
    mc "Her neyse, madem buradayız, neden biraz kitap okumuyoruz?"
    y 2s "Şey..."
    y "Senin için sorun olmadığı sürece."
    mc "Evet."
    mc "Zaten tüm bu olanlardan kafamı kaldırmam gerekiyor."

    # If we haven't seen her exclusive yet, play it now
    if not y_exclusivewatched and poemwinner[2] == "yuri":
        call yuri_exclusive_2_ch3
    else:
        jump ch3_start_none
    return

label ch3_start_none:
    if not renpy.music.get_playing(channel='music') == audio.t3:
        stop music fadeout 1.0
    scene bg club_day
    with wipeleft_scene
    "..."
    if not renpy.music.get_playing(channel='music') == audio.t3:
        play music t3
    show monika 4b at t11 zorder 2
    m "Tamam, millet!"
    "Bir süre geçtikten sonra Monika kulüp odasına seslenir."
    m "Neden şimdi şiirlerimizi paylaşmıyoruz?"
    show monika at thide zorder 1
    hide monika
    "Ne olduğunu anlamadan her şey normale dönüyor."
    "Herkes şiirlerini almaya gidiyor ve ben de aynısını yapıyorum."
    "Monika ile göz göze geldim ve o da bana gülümsedi."
    "Sayori ile ne konuştuğunu merak ediyorum..."
    return


label ch3_end:
    stop music fadeout 1.0
    scene bg club_day
    show monika 4b at t32 zorder 2
    with wipeleft_scene
    play music t3
    m "...Tamam, siz üçünüz!"
    m "Hepimiz şiirleri paylaşmayı bitirdik, değil mi?"
    m "Neden anlamaya başlamıyoruz--"
    show natsuki 3c at f31 zorder 3
    n "Bir saniye bekle!"
    n "Bana mı öyle geliyor, yoksa az önce garip bir şey mi söyledin?"
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m 4d "Eh...?"
    show monika at t32 zorder 2
    show yuri 1e at f33 zorder 3
    y "Bir şeyler biraz alışılmadık geliyordu..."
    y "Bu doğru."
    y 1f "Kulübe hitap ederken her zamanki sloganınızdan saptınız."
    show yuri at t33 zorder 2
    show monika 1n at f32 zorder 3
    m "S-Slogan?"
    m "Benim bir sloganım yok..."
    show monika at t32 zorder 2
    show natsuki 4q at f31 zorder 3
    n "Tanrım..."
    n "Bugün neden bu kadar garip bir ruh hali var?"
    n "Bak, Yuri bile buna bağışık değil."
    show natsuki at t31 zorder 2
    show yuri 4b at f33 zorder 3
    y "Uu..."
    y "Durgun hava, korkunç bir şey olacağının habercisidir..."
    show yuri at t33 zorder 2
    mc "Belki {i}sizin{/i} kitaplarınızda!"
    mc "Bakın, farklı olan tek şey Sayori'nin burada olmaması."
    show yuri 2g at f33 zorder 3
    y "Ah..."
    y "...Görünüşe göre haklısın."
    show yuri at t33 zorder 2
    show monika 2r at f32 zorder 3
    m "İç çek..."
    m 2d "Sayori her zaman havayı biraz olsun yumuşatmaya yardımcı oluyor, değil mi?"
    m "O etrafta olmadığında sanki herkesin dengesi biraz bozuluyor gibi..."
    show monika at t32 zorder 2
    show natsuki 3k at f31 zorder 3
    n "Hangi cehenneme kaçtı bu arada?"
    n "Sadece işemeye gittiğini sanıyordum."
    show natsuki at t31 zorder 2
    show yuri 1l at f33 zorder 3
    y "Natsuki, lütfen biraz nezaket göster..."
    show yuri at t33 zorder 2
    show natsuki 4w at f31 zorder 3
    n "Oh, hadi ama."
    show natsuki at t31 zorder 2
    mc "Ah, aslında kendini pek iyi hissetmiyordu ve eve erken gitti..."
    show yuri 2t at f33 zorder 3
    y "Öyle mi...?"
    y "Umarım iyidir..."
    show yuri at t33 zorder 2
    show natsuki 5c at f31 zorder 3
    n "Ciddi misin?"
    n "Onunla eve gitmemek için onca zaman varken, kendini iyi hissetmediği zamanı mı seçtin?"
    n "İkinizin sevgi dolu olması buraya kadarmış."
    show natsuki at t31 zorder 2
    mc "Ah--hayır!"
    mc "Öncelikle, Sayori ile olan arkadaşlığımı yanlış anlamayı bırak!"
    mc "Ve ikincisi..."
    mc "Bugün benden kaçıyor gibiydi, o yüzden zorlamak istemedim..."
    show yuri 1g at f33 zorder 3
    y "Hooooh?"
    show yuri at t33 zorder 2
    mc "{i}O kadar insan arasından Yuri'den mi geliyor bu tuhaf ifade?{/i}"
    show monika 1r at f32 zorder 3
    m "Calm down, guys...!"
    m 1d "I talked to her earlier, and everything is fine."
    show monika at t32 zorder 2
    mc "What did she say...?"
    show monika at f32 zorder 3
    show yuri 1e
    m 1a "Anyway, we need to figure out the rest of the festival preparations, so..."
    m "Let's decide what everyone will be doing this weekend."
    show monika at t32 zorder 2
    show natsuki 4l at f31 zorder 3
    n "I already know what {i}I'm{/i} doing!"
    show natsuki at t31 zorder 2
    show monika 2b at f32 zorder 3
    m "That's right."
    m "Natsuki will be making cupcakes."
    m "But we might need a lot of them, and different flavors..."
    m "Can you handle that all by yourself, Natsuki?"
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n 4z "Challenge accepted!"
    show natsuki 4a at t31 zorder 2
    show monika at f32 zorder 3
    m 1a "And as for myself..."
    m "I'm going to be printing and assembling all the poetry pamphlets."
    m "Sayori will be helping me design them."
    m "And as for Yuri..."
    m 1d "..."
    m 3n "Yuri, you can..."
    m "Ah... Um..."
    show monika at t32 zorder 2
    show natsuki 3c at f31 zorder 3
    n "...?"
    show natsuki at t31 zorder 2
    show monika 5 at f32 zorder 3
    m "Guys..."
    m "Can you help me come up with something for Yuri...?"
    show monika at t32 zorder 2
    show yuri 4c at f33 zorder 3
    y "I..."
    y "I'm useless..."
    show yuri at t33 zorder 2
    show monika 1g at f32 zorder 3
    m "N-No!"
    m "That's not it at all!"
    m "You're the most talented person here, you know!"
    show monika at t32 zorder 2
    show natsuki 5g at f31 zorder 3
    n "..."
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m 1g "N-Now Natsuki's pouting, too??"
    show monika at t32 zorder 2
    mc "Jeez, even I can tell now..."
    mc "I guess I never gave Sayori enough credit, but I can tell things are even harder on you when she's not around."
    show monika at f32 zorder 3
    m "Ah..."
    m "That may be the case..."
    m 1i "But if I can't also be a leader on my own, then I won't grow as a person."
    m 2i "So, Yuri...!"
    m 2a "You have beautiful handwriting, you know?"
    m "So you should make some banners and decorations to help set the atmosphere."
    show monika at t32 zorder 2
    show yuri 4a at f33 zorder 3
    y "Atmosphere...?"
    y "Um, about that..."
    y "I..."
    y 2r "I love atmosphere!"
    show yuri 2l
    "Yuri's expression suddenly changes as she stares at her desk in focus and starts nodding to herself."
    show yuri at t33 zorder 2
    mc "Your mind is already racing, I see..."
    show monika 2b at f32 zorder 3
    m "That's great!"
    m "You'll be a wonderful help, Yuri."
    m 2a "But anyway..."
    m "That just leaves you, [player]."
    show monika at t32 zorder 2
    mc "The one who is truly useless."
    show monika 1k at f32 zorder 3
    m "Ahaha! Don't say that."
    m 1b "In fact..."
    m "Both Natsuki and Yuri have some pretty heavy tasks to handle."
    m "It would probably go a long way to give one of them a hand."
    m 1m "You could always help me out, as well..."
    m 1a "I would be really appreciative of that."
    show monika at t32 zorder 2
    mc "Ah--"
    mc "That's..."
    "Is Monika suggesting I spend the weekend with one of my club members?"
    "How on Earth are they going to respond to a suggestion like that...?"
    show yuri 1u at f33 zorder 3
    y "Ah..."
    y "I...suppose I wouldn't mind a bit of help..."
    show yuri at t33 zorder 2
    show natsuki 3c at f31 zorder 3
    n "Well, even if you don't know how to bake, there's always some dirty work I could give to you."
    n 3q "It's not like Monika's going to give me a choice, and you shouldn't be sitting on your butt anyway..."
    "Natsuki tries to mumble a bunch of excuses like that."
    show natsuki at t31 zorder 2
    show yuri 2k at f33 zorder 3
    y "Um..."
    y "If I recall, Natsuki..."
    y 2f "You mentioned that you would like to handle the baking on your own."
    y "[player] may not like to be around if you only make him out to be a nuisance..."
    y 2i "So therefore..."
    y "He may be more suited to assisting with the decorations."
    show yuri at t33 zorder 2
    show natsuki 4e at f31 zorder 3
    n "Hold on! I never said that!"
    n 4h "How hard could it be to make a few decorations, anyway?"
    n "Sounds more like you're just making excuses for [player] to--"
    show natsuki at t31 zorder 2
    show yuri 3n at f33 zorder 3
    y "W-What are you saying?!"
    y "It will be extremely meticulous work..."
    show yuri at t33 zorder 2
    show natsuki 5w at f31 zorder 3
    n "And baking isn't?"
    n "Just what do you think--"
    show natsuki at t31 zorder 2
    show monika 2g at f32 zorder 3
    m "Guys, guys!"
    m "Let's settle down for a moment..."
    m 2d "In the end, I think it's up to [player] to decide how he'd like to contribute."
    m "Besides..."
    m 5 "He hasn't really gotten the chance to spend any time with me yet, you know?"
    m "So I'm sure he's interested in--"
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n 4f "You {i}literally{/i} just said--"
    show natsuki at t31 zorder 2
    show yuri 2h at f33 zorder 3
    y "I-I'm surprised as well!"
    show yuri at t33 zorder 2
    show monika 1l at f32 zorder 3
    m "Sorry, sorry!"
    m "I was just saying, though..."
    show monika at t32 zorder 2
    show natsuki 4x at f31 zorder 3
    n "Jeez..."
    n "Can we just settle this already?"
    show natsuki at t31 zorder 2
    show monika 1e at f32 zorder 3
    m "Yeah..."
    m "[player], you're okay with this, right?"
    m 1a "In the end, it's up to you."
    show monika at t32 zorder 2
    mc "Ah..."
    mc "Of course."
    show natsuki 5g at f31 zorder 3
    n "Hmph."
    show natsuki at t31 zorder 2
    show yuri 1f at f33 zorder 3
    y "Very well..."
    show yuri at t33 zorder 2
    show monika 1a at f32 zorder 3
    m "In that case..."
    show monika at t32 zorder 2
    "Everyone looks straight at me."
    menu:
        "But of course, I'm going to go with--"
        "Natsuki." if yuriRoutePoints <= 1:
            call ch3_end_natsuki
        "Yuri." if natsukiRoutePoints <= 1: # this way they become options if going for Sayori
            call ch3_end_yuri
        "Monika.":
            call ch3_end_monika
        "Sayori...":
            call ch3_end_sayori
    scene bg residential_day
    with wipeleft_scene
    $ ch4_name = ch4_scene.capitalize()
    "I can't believe this!"
    "[ch4_name] is going to be coming to my house on Sunday...?"
    if help_sayori:
        "Even though I would have preferred to do this with Sayori..."
        "My anxiety still shoots through the roof."
        "I guess I've gotten pretty used to handling her at this point..."
        "But who knows what might end up happening when we're outside of school?"
        "She even told me she was looking forward to it..."
        "I shake my head."
        "Why do I feel nervous that Sayori finds out about this?"
        "It's not like we feel {i}that{/i} way about each other..."
        "Besides, like Monika said, this is about the club."
        "I have nothing to worry about."
        "If I just go with it, then I'll have a good time."
    else:
        "My anxiety shoots through the roof."
        "Even though I've gotten pretty used to handling her at this point..."
        "There's no telling what might end up happening when we're outside of school."
        "More than that...she told me that she was looking forward to it."
        "Is this the chance I have to make something happen between us?"
        "...Or is it too early for that?"
        "Only time will tell..."
        "But until then, I won't be able to take my mind off it."
        "I seriously can't wait!"
    return

label ch3_end_sayori:
    $ help_sayori = True
    mc "I mean..."
    mc "If it's going to be anyone, then I prefer helping Sayori."
    mc "I mean, we're already neighbors, and--"
    show yuri 2f at f33 zorder 3
    y "But Monika said--"
    show yuri at t33 zorder 2
    show natsuki 4w at f31 zorder 3
    n "Monika said that Sayori was helping her!"
    n "Jeez..."
    n 4h "Do you really hate us that much?"
    show natsuki at t31 zorder 2
    mc "N-No!"
    show monika 1e at f32 zorder 3
    m "Sorry, I didn't mean for this to be difficult..."
    show monika at t32 zorder 2
    menu:
        m "Just think of the club, okay?"
        "Natsuki." if yuriRoutePoints <= 1:
            call ch3_end_natsuki
        "Yuri." if natsukiRoutePoints <= 1: # this way they become options if going for Sayori
            call ch3_end_yuri
        "Monika." if help_monika is False:
            call ch3_end_monika
    return


label ch3_end_monika:
    $ help_monika = True
    mc "Well, I guess I should probably be helping Monika..."
    show monika 5 at f32 zorder 3
    m "Yay, you picked me!"
    show monika at t32 zorder 2
    show natsuki 3e at f31 zorder 3
    stop music fadeout 1.0
    n "Hold on one second!"
    show natsuki at t31 zorder 2
    show yuri 2r at f33 zorder 3
    y "Y-Yeah!"
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    play music t7
    n "Monika, you're the one who needs the least help out of all of us!"
    show natsuki at t31 zorder 2
    show monika 1d at f32 zorder 3
    m "Eh? But..."
    show monika at t32 zorder 2
    show yuri 1h at f33 zorder 3
    y "I agree with Natsuki."
    y 1l "Not only is your work already most suitable for one person..."
    y "But you already have Sayori as well."
    show yuri at t33 zorder 2
    show monika at f32 zorder 3
    m 1p "But [player] was the one who..."
    m "Ah..."
    show monika at t32 zorder 2
    show natsuki 3c at f31 zorder 3
    n "That doesn't matter."
    n "You were the one who scared him into picking you in the first place."
    n 3e "You're the club president, Monika."
    n "You're supposed to make responsible decisions for the club!"
    show natsuki at t31 zorder 2
    show yuri 2f at f33 zorder 3
    y "Monika, you shouldn't let any ulterior motives interfere with this decision."
    show yuri at t33 zorder 2
    show monika 2i at f32 zorder 3
    m "Ulterior motives?"
    m "W-What are you saying, Yuri?"
    m "In fact, it sounds like you guys are the ones with ulterior motives!"
    show monika at t32 zorder 2
    show natsuki 1x at f31 zorder 3
    n "{i}Excuse{/i} me?"
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m "Otherwise...this wouldn't have been made into such a big deal in the first place!"
    show monika at t32 zorder 2
    show yuri 3r at f33 zorder 3
    y "That's...completely false, Monika!"
    show yuri at t33 zorder 2
    show natsuki 3e at f31 zorder 3
    n "Yeah!"
    n "We have a lot of work to do, you know!"
    n "We won't do as good of a job if you make us work alone."
    show natsuki at t31 zorder 2
    show monika 1p at f32 zorder 3
    m "Ah...maybe...that's true..."
    show monika at t32 zorder 2
    show yuri 2l at f33 zorder 3
    y "Think of the club, Monika..."
    y "If we want our event to succeed, then we need to appropriately distribute our resources."
    show yuri at t33 zorder 2
    show monika 3n at f32 zorder 3
    m "Um..."
    m "Ah..."
    show monika at t32 zorder 2
    show natsuki 4x at f31 zorder 3
    n "So are you going to do the right thing, {i}President{/i}?"
    show natsuki at t31 zorder 2
    show monika 1p at f32 zorder 3
    m "Okay, okay!"
    m "I get it!"
    stop music fadeout 1.0
    show natsuki 4g
    show yuri 2g
    m 1r "Sigh..."
    m 1g "It's...technically most logical for [player] to help one of you two."
    m "So..."
    m 1c "I guess...that's what we'll do."
    show monika at t32 zorder 2
    play music t3
    menu:
        m "Do you have a preference, [player]?"
        "Natsuki." if yuriRoutePoints <= 1:
            call ch3_end_natsuki
        "Yuri." if natsukiRoutePoints <= 1: # this way they become options if going for Sayori
            call ch3_end_yuri
        "Sayori..." if help_sayori is False:
            call ch3_end_sayori
    return

label ch3_end_natsuki:
    $ch4_scene = "natsuki"
    mc "Well, baking sounds like it could be fun..."
    mc "And you guys made it sound like a lot of work, so it could probably use two people."
    show natsuki 4l at f31 zorder 3
    n "Don't worry!"
    n "Baking is a ton of fun!"
    n "You'll definitely agree!"
    show natsuki at t31 zorder 2
    show monika 1d at f32 zorder 3
    m "Eh?"
    m "Just a minute ago you were saying that--"
    show monika at t32 zorder 2
    show natsuki 1q at f31 zorder 3
    n "Th-That's because--!"
    n "..."
    n "...Never mind, okay?"
    show natsuki at t31 zorder 2
    show monika 2a at f32 zorder 3
    m "Well, anyway..."
    m "You'll be fine by yourself, right, Yuri?"
    show monika at t32 zorder 2
    show yuri 1j at f33 zorder 3
    y "Of course."
    y "I'm used to it, after all..."
    show yuri 1g at t33 zorder 2
    show monika 1e at f32 zorder 3
    m "..."
    show monika at t32 zorder 2
    mc "..."
    show monika at f32 zorder 3
    m 1n "That's...good..."
    "Even though Yuri is being melodramatic, it's a little hard to not feel bad..."
    show monika 1m at t32 zorder 2
    show natsuki 3a at f31 zorder 3
    n "So, that's everything, right?"
    n "Anything else we need to talk about?"
    show natsuki at t31 zorder 2
    show monika 1a at f32 zorder 3
    m "No, I think that's it..."
    m "Are you guys excited?"
    show monika at t32 zorder 2
    show natsuki 1z at f31 zorder 3
    n "Yes!"
    n "Everything except the performance is gonna be awesome!"
    show natsuki 1a at t31 zorder 2
    mc "I don't think that really counts..."
    show monika at f32 zorder 3
    m "What about you, [player]?"
    show monika at t32 zorder 2
    mc "Me?"
    mc "Ah, I guess you could say I'm interested to see how it'll turn out..."
    show monika 2b at f32 zorder 3
    m "That's good enough for me!"
    m "What about you, Yuri?"
    m 2d "...Yuri?"
    show monika at t32 zorder 2
    show natsuki 3c at f31 zorder 3
    n "She's still sulking."
    show natsuki at t31 zorder 2
    show yuri 4b at f33 zorder 3
    y "..."
    show yuri at t33 zorder 2
    show natsuki 5n at f31 zorder 3
    n "..."
    "Natsuki starts pouting, too."
    n "It's not..."
    n 5m "I mean, it's not that big of a deal or anything..."
    show natsuki at t31 zorder 2
    mc "Well, it might not be just that..."
    show natsuki at f31 zorder 3
    n "...?"
    show natsuki at t31 zorder 2
    mc "I think that Yuri might just be feeling a little underappreciated in general."
    mc "Having to come up with something for her to do, and then nobody offering to help."
    show natsuki at f31 zorder 3
    n 5q "That doesn't mean..."
    n "..."
    n 5r "Uu..."
    show natsuki 5u
    "Natsuki glances back and forth between everyone with a worried expression."
    n 1u "Look..."
    "Natsuki goes over and puts her hands down on Yuri's shoulders."
    n 1h "Yuri."
    n "You really are the most talented one here."
    n "And..."
    n "And you're going to help make the event a lot more fun and welcoming."
    n 1q "I mean, the cupcakes will probably help a lot too..."
    n 3h "But you're gonna make the atmosphere special."
    n "That'll be really important for the way that people feel during the performances."
    n "So..."
    n 4w "You need to stop being dumb and give yourself a little more credit!"
    "Natsuki releases her hands and turns around to face the other direction."
    show natsuki at t31 zorder 2
    show yuri at f33 zorder 3
    y 4a "..."
    y "You didn't...really mean that, did you?"
    show yuri at t33 zorder 2
    show natsuki 5u at f31 zorder 3
    n "Um..."
    n "N-Not really, but..."
    show natsuki at t31 zorder 2
    "Yuri isn't the only one surprised."
    "Monika and I are also taken aback by Natsuki's words."
    "Natsuki, of all people, to be saying such encouraging things."
    "But I begin to understand."
    "Natsuki was trying to sound like Sayori."
    "Even if it didn't work perfectly, I can tell that she tried to say something Sayori would say at a time like this."
    "Because Sayori always helps everyone smile and feel good about themselves."
    show yuri 2l at f33 zorder 3
    y "I'm sorry...for being dumb."
    y 2i "I'm going to do my best."
    y "And all of us are going to make it a really great event."
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 5j "Yeah."
    show natsuki at t31 zorder 2
    show monika 2k at f32 zorder 3
    m "Yeah!"
    m 2b "I hope to see everyone do their best."
    m "But with that..."
    m "There's nothing more for today."
    m "So I guess it's time for us to head out."
    show monika at t32 zorder 2
    show natsuki 3k at f31 zorder 3
    n "Okay, but I'm staying here a bit longer."
    n "I barely got to do any reading today, so..."
    show natsuki at t31 zorder 2
    show monika 2a at f32 zorder 3
    m "Fair enough, there's nothing wrong with that."
    hide natsuki
    hide monika
    hide yuri
    with wipeleft
    "Everyone packs up their things."
    "I start to follow Monika and Yuri out the door as they chat between each other."
    play music t6 fadeout 1
    show natsuki 4g at t11 zorder 2
    n "Um, where are you going?"
    mc "Eh...?"
    n 4c "We still need to figure out our plans for this weekend."
    n "You literally would've gotten home and realized that you didn't even have a way to contact me."
    mc "Oh, that's true..."
    mc "I have no idea how that slipped my mind."
    n 2c "Jeez, good thing I stopped you."
    n "I'm giving you my number, okay?"
    n 2q "You'd better not make it weird or anything!"
    mc "Why would I do that...?"
    n 1s "Hmph..."
    "Natsuki gives me her number."
    n 1c "Okay..."
    n "I'm coming over on Sunday."
    n "I'll bring all the ingredients."
    mc "Wait!"
    mc "You're coming to {i}my{/i} house?"
    n 2c "Well, yeah."
    n "What's wrong with that?"
    mc "I mean..."
    mc "I just figured that since I'm the one helping, I would be going to your house..."
    n "Yeah, right."
    n 5x "Like I could have a guy over my house..."
    n "My dad would kill me."
    mc "Really?"
    mc "That's kinda strict, if you ask me."
    n 5r "Yeah, how do you think I feel?"
    n "I can't do anything when my dad is home..."
    n 2q "Anyway...I just needed to complain for a second."
    n 2c "We have each other's numbers now."
    n "That's all I needed from you."
    n "I guess I'll text you when I'm coming over."
    mc "Alright, fine by me."
    n "Yeah."
    n 4a "I'm really gonna show you why I love baking so much."
    n "So you'd better look forward to it."
    mc "Oh?"
    mc "Didn't you say you were just going to give me the dirty work?"
    n 1r "Well--!"
    n "I was...just saying that."
    n 1q "It's not like I could act like...in front of everyone..."
    n "That I was looking forward to this."
    mc "Wait, really??"
    n 5w "Well, kind of!"
    n "Just because...I never got to bake with someone else before."
    n 5h "That's all it is, so..."
    mc "Alright, I get it."
    mc "Sorry for overreacting."
    mc "Anyway, I'll be heading out now."
    mc "See you on Sunday."
    n 5m "Ah--"
    n "..."
    n 5u "...Never mind."
    return

label ch3_end_yuri:
    $ ch4_scene = "yuri"
    mc "Well, I'll probably be most useful helping out Yuri..."
    show yuri at f33 zorder 3
    y 2n "M-Me...?"
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 4e "Are you serious?"
    n "Why would you--"
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m 1i "Natsuki."
    m "I can already tell you're about to say something mean."
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n 5r "N-No..."
    n "I was just saying--"
    n "Ugh..."
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m 2a "So, you'll be helping Yuri then, [player]?"
    show monika at t32 zorder 2
    mc "Yeah."
    mc "That's what I'm going to do."
    show yuri at f33 zorder 3
    y 1u "I'm glad."
    y "I have a bad habit of overthinking these sorts of things..."
    y "So I think your assistance will be very useful."
    show yuri at t33 zorder 2
    show monika at f32 zorder 3
    m "That's great to hear."
    m "Natsuki, will you be able to handle the baking yourself?"
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n 3w "I mean, yeah."
    n "I already said I would be fine."
    show natsuki at t31 zorder 2
    show monika at f32 zorder 3
    m 1i "Okay, okay..."
    "Everyone can tell that Natsuki is feeling a little sour."
    show monika at t32 zorder 2
    mc "So...is that everything we needed to go over?"
    show monika at f32 zorder 3
    m 1a "Yeah, that should be about it."
    m 2a "Are you guys excited?"
    show monika at t32 zorder 2
    show yuri at f33 zorder 3
    y 1i "Well, 'excited' may not be the right word..."
    y "But I suppose I'm looking forward to it a little bit."
    show yuri at t33 zorder 2
    show monika at f32 zorder 3
    m "Do you feel the same way, [player]?"
    show monika at t32 zorder 2
    mc "Me?"
    mc "Ah, I guess you could say I'm interested to see how it'll turn out..."
    show monika at f32 zorder 3
    m 2b "That's good enough for me!"
    m 2a "What about you, Natsuki?"
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n 5s "..."
    show monika at t32 zorder 2
    show yuri at f33 zorder 3
    y 2h "Natsuki!"
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 1o "What??"
    n 1m "Why is everyone yelling at me?"
    n "I didn't even do anything...!"
    show natsuki 1n at t31 zorder 2
    show yuri at f33 zorder 3
    y 3n "N-no--!"
    y "That's not what I meant at all!"
    y 3o "A-Ah..."
    "Yuri anxiously glances between everyone in the room."
    y 2w "I-I'm sorry for this!"
    y 2v "I don't really know why [player] picked me..."
    y "And also..."
    y 2t "Your cupcakes are the best cupcakes I've ever had!"
    y "They go really well with my tea!"
    y "And nothing that I do for the event will compare to that, so..."
    y 4b "So..."
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 3q "I get it, I get it."
    n 3h "I'm kinda surprised, though..."
    show natsuki at t31 zorder 2
    show yuri at f33 zorder 3
    y "W-Why?"
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 3q "Um..."
    n "Well, I'm the one acting immature..."
    n "I already know that."
    n 5h "But you're trying to cheer me up all of a sudden..."
    show natsuki at t31 zorder 2
    show yuri at f33 zorder 3
    y "I-I know I'm not very good at it..."
    y 2v "I'm sorry if I said something bad!"
    "Natsuki isn't the only one surprised."
    "Monika and I are also taken aback by Yuri's words."
    "When she already has trouble with words, trying to cheer someone up must be far out of her own comfort zone."
    "But I begin to understand."
    "Yuri was trying to sound like Sayori."
    "Even if it didn't work perfectly, I can tell that she tried to say something Sayori would say at a time like this."
    "Because Sayori always helps everyone smile and feel good about themselves."
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 1h "No..."
    n "I kinda appreciated it."
    n 1u "I'm sorry...for making a big deal out of nothing."
    n 1m "But I'm going to say this."
    show natsuki at t31 zorder 2
    show yuri at f33 zorder 3
    y 2e "...?"
    show yuri at t33 zorder 2
    show natsuki at f31 zorder 3
    n 4e "You better bet that my cupcakes are going to be the best part of the whole event!"
    show natsuki 4a at t31 zorder 2
    show yuri at f33 zorder 3
    y 2f "Ah..."
    y 1s "...I believe you."
    show yuri at t33 zorder 2
    show monika at f32 zorder 3
    m 2b "Yeah!"
    m "I hope to see everyone do their best."
    m "But with that..."
    m "There's nothing more for today."
    m "So I guess it's time for us to head out."
    show monika at t32 zorder 2
    show natsuki at f31 zorder 3
    n "Alright, let's get out of here, then."
    hide monika
    hide natsuki
    hide yuri
    with wipeleft
    "Everyone packs up their things."
    "I start to follow Monika and Natsuki out the door as they chat between each other."
    play music t6 fadeout 1
    show yuri 2t at t11 zorder 2
    y "U-Um--!"
    mc "Eh?"
    "I turn around."
    y "Sorry..."
    y 2s "I realized that I don't have any way of contacting you this weekend..."
    mc "Oh, you're right."
    mc "I can't believe that slipped my mind."
    mc "Should I give you my phone number?"
    y 1a "I think...that would be the best way, yes."
    mc "Alright, then..."
    "Yuri and I exchange phone numbers."
    y "Okay."
    y "Then, I'll be stopping by your house on Sunday..."
    mc "Eh?"
    mc "My house?"
    y 2t "I-Is that a problem...?"
    mc "No, not at all..."
    mc "I just thought that I would be the one going to your house, since I'm the one helping you."
    y "Ah, I suppose that makes sense..."
    y "But, if you don't mind..."
    y 1u "I think I would prefer going to your house."
    mc "Alright."
    mc "In that case, it won't be a problem."
    "I decide not to press Yuri for a reason."
    "It's not like it should matter much either way, so I'll just need to make sure my room is clean."
    mc "I hope I manage to make myself useful in some way..."
    mc "I'm not nearly as creative as you are."
    y 1a "Don't underestimate yourself, [player]."
    y "I think that we'll make a very productive team."
    y 1u "Even if you only chose me because you felt bad or something..."
    mc "Wait...!"
    mc "You don't actually think that, do you?"
    y 4b "..."
    y "I...don't know."
    y "It's difficult to come up with any other reason you may have chosen me..."
    mc "You're forgetting the one reason with the most common sense!"
    mc "I chose to help you because that's what I want to do."
    y 2v "B-But..."
    "Yuri thinks to herself with an extremely tense expression."
    mc "Yuri...you're overthinking this."
    mc "You wanted me to point out when you're overthinking, right?"
    y "Eh...?"
    y 4 "I...didn't realize..."
    mc "I'm telling you, I want to."
    mc "That's all there is to it."
    mc "Do you believe me?"
    y 1t "I..."
    "Yuri thinks really hard again."
    "She looks straight into my eyes for a long while."
    y 3l "...I believe you!"
    "As if it took her tremendous effort, Yuri finally says that and relaxes her expression."
    y 3s "And I'm really looking forward to Sunday."
    mc "Yeah..."
    mc "I am too."
    show yuri at thide zorder 1
    hide yuri
    "After that exchange, I make my way out the door, and Yuri follows."
    return
